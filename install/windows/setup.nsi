; Script generated by the HM NIS Edit Script Wizard.

SetCompressor /FINAL /SOLID lzma
SetCompressorDictSize 64

!define MULTIUSER_EXECUTIONLEVEL "Admin"
!include MultiUser.nsh

; HM NIS Edit Wizard helper defines
; These ones are defined via the command line (to select Delphi/Odemis Viewer)
;!define PRODUCT_NAME "OdemisViewer"
;!define PRODUCT_HNAME "Odemis Viewer"
; Product version is defined through the command line!
!define PRODUCT_PUBLISHER "Delmic"
!define PRODUCT_WEB_SITE "${WEBSITE}"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_EXECUTABLE "${PRODUCT_NAME}.exe"

; MUI 1.8 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "odemis-viewer.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "..\..\image\${IMAGE}"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
!define MUI_TEXT_WELCOME_INFO_TITLE "$(^NameDA)"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!define MUI_FINISHPAGE_NOAUTOCLOSE
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\${PRODUCT_NAME}.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; File association. For information see
; http://nsis.sourceforge.net/File_Association
; http://nsis.sourceforge.net/FileAssoc
; https://msdn.microsoft.com/en-us/library/cc144175(VS.85).aspx
; https://msdn.microsoft.com/en-us/library/windows/desktop/hh127445(v=vs.85).aspx
!include "FileAssociation.nsh"
; FIXME: only include/use this one
!include "FileAssoc.nsh"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "dist/${PRODUCT_NAME}-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$APPDATA\${PRODUCT_NAME}"
  SetOutPath "$INSTDIR"
  File /r .\dist\${PRODUCT_NAME}\*.*
  CreateDirectory "$SMPROGRAMS\${PRODUCT_HNAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_HNAME}\${PRODUCT_HNAME}.lnk" "$INSTDIR\${PRODUCT_NAME}.exe"
  CreateShortCut "$DESKTOP\${PRODUCT_HNAME}.lnk" "$INSTDIR\${PRODUCT_NAME}.exe"

  ${registerExtension} "$INSTDIR\${PRODUCT_NAME}.exe" ".tiff" "Odemis.TIFF"
  ${registerExtension} "$INSTDIR\${PRODUCT_NAME}.exe" ".tif" "Odemis.TIFF"
  ${registerExtension} "$INSTDIR\${PRODUCT_NAME}.exe" ".h5" "Odemis.HDF5"
  ${registerExtension} "$INSTDIR\${PRODUCT_NAME}.exe" ".hdf5" "Odemis.HDF5"
  ; Tell the Windows shell to recheck the registry.
  !insertmacro UPDATEFILEASSOC
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_HNAME}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_HNAME}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\${PRODUCT_NAME}.exe"

  ; User friendly app name (without .exe)
  ;WriteRegStr HKCR "Applications\${PRODUCT_NAME}.exe" "" "${PRODUCT_HNAME}"
  WriteRegStr HKCR "Applications\${PRODUCT_NAME}.exe" "DefaultIcon" "$INSTDIR\${PRODUCT_NAME}.exe"
  WriteRegStr HKCR "Applications\${PRODUCT_NAME}.exe" "FriendlyAppName" "${PRODUCT_HNAME}"

  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\fabrixkassa.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) successfully removed."
FunctionEnd

Function .onInit
  !insertmacro MULTIUSER_INIT

  ; http://nsis.sourceforge.net/KillProc_plug-in
  StrCpy $0 ${PRODUCT_EXECUTABLE}
  KillProc::FindProcesses

  StrCmp $1 "-1" wooops     ; Error occured
  StrCmp $0 "0" completed   ; None found, so done
  Sleep 1500

  StrCpy $0 ${PRODUCT_EXECUTABLE}
  KillProc::KillProcesses

  StrCmp $1 "-1" wooops     ; Error during kill
  Goto completed            ; Kill done

  wooops:
  MessageBox MB_OK|MB_ICONEXCLAMATION "Error while stopping ${PRODUCT_HNAME}" /SD IDOK
  Abort

  completed:
  DetailPrint "Done."
FunctionEnd

Function un.onInit
  !insertmacro MULTIUSER_UNINIT
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to uninstall $(^Name)?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$SMPROGRAMS\${PRODUCT_HNAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_HNAME}\Website.lnk"
  Delete "$DESKTOP\${PRODUCT_HNAME}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_HNAME}\\${PRODUCT_HNAME}.lnk"

  RMDir /r "$SMPROGRAMS\${PRODUCT_HNAME}"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true

  ${unregisterExtension} ".tiff" "Odemis.TIFF"
  ${unregisterExtension} ".tif" "Odemis.TIFF"
  ${unregisterExtension} ".h5" "Odemis.HDF5"
  ${unregisterExtension} ".hdf5" "Odemis.HDF5"
  !insertmacro UPDATEFILEASSOC
SectionEnd
